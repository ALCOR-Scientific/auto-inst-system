#System installation for CM-T335

# Next run parameters
setenv blue_can blue
setenv disp_type lcd
setenv fdtaddr 0x83000000
setenv delete_dvi 'fdt rm /panel/display-timings/dvi'
setenv delete_lcd 'fdt rm /panel/display-timings/lcd'
setenv delete_lvds 'fdt rm /panel/display-timings/lvds'
setenv test_lcd 'if test $disp_type = lcd; then run delete_dvi; run delete_lvds; fi'
setenv test_dvi 'if test $disp_type = dvi; then run delete_lcd; run delete_lvds; fi'
setenv test_lvds 'if test $disp_type = lvds; then run delete_lcd; run delete_dvi; fi'
setenv update_display 'run test_lcd; run test_dvi; run test_lvds'
setenv delete_can 'fdt rm d_can0; fdt rm d_can1'
setenv delete_blue 'fdt set serial1 status disab; fdt rm /ocp/spi/wlcore'
setenv update_blue_can 'if test $blue_can = blue; then run delete_can; fi; if test $blue_can = can; then run delete_blue; fi'
setenv load_z_nand 'nand read ${loadaddr} 900000 400000 && nand read ${fdtaddr} 400000 10000'
setenv update_fdt 'fdt addr ${fdtaddr}; run update_display; run update_blue_can'
setenv bootcmd 'run load_z_nand && run update_fdt; bootz ${loadaddr} - ${fdtaddr}'
setenv bootargs "console=${console} root=${nandroot} rootfstype=${nandrootfstype} ubi.mtd=${rootfs_name}" 
saveenv

# Parameters
setenv rdaddr 0x81000000
setenv mmcdev 0:1
setenv tty ${console}
setenv board_param nand=3:0:5:0:6:0:rootfs
